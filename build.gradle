plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.2'
    id 'io.spring.dependency-management' version '1.1.2'
    id 'org.asciidoctor.jvm.convert' version '3.3.2' // asciidoctor 플러그인 추가

    // rest docs + swagger
    id 'com.epages.restdocs-api-spec' version '0.18.2'
    id 'org.hidetake.swagger.generator' version '2.18.2'
}

group = 'com.programmers'
version = '1.0.0'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt // asciidoctorExt을 Configuration 지정
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    runtimeOnly 'com.h2database:h2'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor' // adoc 파일에서 사용할 snippets 속성이 자동으로 build/generated-snippets를 가리키도록 해줌
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    // rest docs + swagger
    testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.18.2'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'
}

ext {
    set('snippetsDir', file("build/generated-snippets")) // snippets 파일이 저장될 경로를 설정
}

test {
    outputs.dir snippetsDir // 테스트 이후 generated-snippets 폴더에 adoc 파일 생성
    useJUnitPlatform()
}

asciidoctor {
    dependsOn test // test 이후 asciidoctor를 진행하도록 설정
    inputs.dir snippetsDir // generated-snippets 폴더에 있는 adoc 파일을 사용
    configurations 'asciidoctorExt' // asciidoctor에서 asciidoctorExt를 configuration으로 사용하도록 설정
    baseDirFollowsSourceFile() // adoc 파일에서는 다른 .adoc 파일을 include하여 사용할 수 있는데, 그럴 경우 경로를 동일하게 baseDir로 설정 (Gradle 7부터 명시)
}

bootJar {
    dependsOn asciidoctor
    dependsOn(':openapi3')

    from("${asciidoctor.outputDir}") {
        into 'static/docs' // jar 파일 내에 static/docs 폴더를 만들어 html 파일 복사 (배포 파일 실행 시 docs 문서 볼 수 있음)
    }
}

openapi3 {
    server = "http://localhost:8080"
    title = "JPA Board API Document"
    description = "Spring Rest Docs with Swagger"
    version = "1.0.0"
    outputFileNamePrefix = 'v1.0.0'
    format = 'json'
    outputDirectory = 'build/resources/main/static/docs'
}